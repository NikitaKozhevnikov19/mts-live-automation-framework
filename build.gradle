plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version "8.4"
}

group = 'kozherka.ru'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

// Версии для UI и Mobile
def restAssuredVersion = "5.4.0"
def allureVersion = "2.25.0"
def selenideVersion = "7.2.3"
def appiumVersion = "9.2.2"

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    // Оставляем для видео Browserstack и работы с JSON
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.16.1"

    // UI (Selenide)
    testImplementation "com.codeborne:selenide:$selenideVersion"

    // MOBILE (Appium)
    testImplementation "io.appium:java-client:$appiumVersion"

    // INFRASTRUCTURE & CONFIG
    testImplementation "org.aeonbits.owner:owner:1.0.12"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"
    testImplementation "com.github.javafaker:javafaker:1.0.2"
    testImplementation "org.assertj:assertj-core:3.25.3"
    testImplementation "org.slf4j:slf4j-simple:2.0.12"

    // ALLURE
    testImplementation "io.qameta.allure:allure-selenide:$allureVersion"
    testImplementation "io.qameta.allure:allure-junit5:$allureVersion"
    testImplementation "org.aspectj:aspectjweaver:1.9.22"
}

allure {
    report { version.set(allureVersion) }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 { adapterVersion.set(allureVersion) }
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform {
        includeTags System.getProperty("groups", "all")
    }

    systemProperties(System.getProperties())
    systemProperty "file.encoding", "UTF-8"

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
